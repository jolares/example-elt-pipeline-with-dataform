config {
  type: "table",
  schema: "reporting",
  tags: ["daily"],
  description: "Creates a summary table for all users including statistics on questions, answers and badges received.",
  assertions: {
    uniqueKey: ["user_id"],
    rowConditions: ["badge_count >= 0"]
  }
}

SELECT
  users.user_id,
  users.age,
  users.creation_date,
  users.user_tenure,
  count(distinct badges.badge_id) as badge_count,
  count(distinct posts_all.post_id) as questions_and_answer_count,
  count(distinct if(type="question", posts_all.post_id, null)) as question_count,
  count(distinct if(type="answer", posts_all.post_id, null)) as answer_count,
  max(badges.award_timestamp) as last_badge_received_at,
  max(posts_all.created_at) as last_posted_at,
  max(if(type="question", posts_all.created_at, null)) as last_question_posted_at,
  max(if(type="answer", posts_all.created_at, null)) as last_answer_posted_at
FROM
  ${ref("users")} as users
  left join ${ref("badges")} as badges
    on users.user_id = badges.user_id
  left join ${ref("posts_combined")} as posts_all
    on users.user_id = posts_all.owner_user_id
GROUP BY
  1,2,3,4
